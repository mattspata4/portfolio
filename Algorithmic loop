(
SynthDef(\oprah,{arg freq=440, amp=1;
        var out;
	    var env= Env.new([0.01, 0.5, 0.5,  0.01], [1.2, 0.4, 0.1], 'exp');
        out=Mix.fill(15,
                {LFPulse.ar(
                        freq*Rand(0.99,1.01),
                        mul:0.011*2,
			            add:0.011.neg,
                        iphase:Rand(0.01, 1)
                )}
        );
        out = Pan2.ar(out, 0.0);
	out = out*EnvGen.kr(env, doneAction:2);

   Out.ar(0, out*amp)
}).add;

SynthDef(\iyanla,{
		arg freq=440, eam=0.8, release=2, amp=0.05;
		var out;
		var env= Env.new([eam, 0.01], [release], 'exp');
		var triangle = LFTri.ar(freq,0,SinOsc.kr(8,0,0.4,0.5));
		out=Pan2.ar(triangle, SinOsc.kr(4,0,1));
		out = out*EnvGen.kr(env, doneAction:2);
		out=out*amp;
		Out.ar(0, out)
}).add
)


(
Pbind(\instrument, \oprah,
	\freq, Pseq([[50, 52, 54, 57, 60]-12,[50, 52, 54, 57, 60]-12,[49, 53, 61, 71]-12,[49, 53, 61, 71]-12].midicps, inf),
\dur, 5.2,
\amp, 0.4).play;


Pbind(\instrument, \oprah,
	\freq, Prand(([88, 88, 90, 90, 83]-12).midicps, inf),
	\dur, Prand([0.2575, 0.515, 0.12875, 0.12875]*2, inf),
	\amp, 0.3
).play;


Pbind(\instrument, \iyanla,
	\dur, 5.2,
	\amp, 0.7,
	\freq, 69).play;


Pbind(\instrument, \iyanla,
	\dur, Pwhite(0.05, 0.2, inf), //Prand([0.065*2, 0.0325*2,0.0975*2, 0.04875], inf)
	\release, 1,
	\amp, 0.03,
	\freq, Pwrand([ 97, 100, 104, 107, 111, 114 ].midicps, [8, 6, 10, 8, 4, 2].normalizeSum, inf)).play;
)
